import {
  core.int.show {show-int},
  core.text {format},
}

data color {
| Black
| Red
| Green
| Yellow
| Blue
| Magenta
| Cyan
| White
}

define _get-color-code(c: color): int {
  match c {
  | Black =>
    0
  | Red =>
    1
  | Green =>
    2
  | Yellow =>
    3
  | Blue =>
    4
  | Magenta =>
    5
  | Cyan =>
    6
  | White =>
    7
  }
}

define get-foreground-tag(c: color): text {
  let color-code = _get-color-code(c) in
  format("\u{1b}[{}m", [show-int(add-int(30, color-code))])
}

define get-intense-foreground-tag(c: color): text {
  let color-code = _get-color-code(c) in
  format("\u{1b}[{}m", [show-int(add-int(90, color-code))])
}

define get-background-tag(c: color): text {
  let color-code = _get-color-code(c) in
  format("\u{1b}[{}m", [show-int(add-int(40, color-code))])
}

define get-intense-background-tag(c: color): text {
  let color-code = _get-color-code(c) in
  format("\u{1b}[{}m", [show-int(add-int(100, color-code))])
}

define set-foreground-color(c: color): unit {
  let tag = get-foreground-tag(c) in
  let _ on tag = print(tag) in
  let _ = tag in
  Unit
}

define set-intense-foreground-color(c: color): unit {
  let tag = get-intense-foreground-tag(c) in
  let _ on tag = print(tag) in
  let _ = tag in
  Unit
}

define set-background-color(c: color): unit {
  let tag = get-background-tag(c) in
  let _ on tag = print(tag) in
  let _ = tag in
  Unit
}

define set-intense-background-color(c: color): unit {
  let tag = get-intense-background-tag(c) in
  let _ on tag = print(tag) in
  let _ = tag in
  Unit
}

define zen(): unit {
  set-intense-foreground-color(Black);
  set-background-color(White);
  print("hello, \n");
  set-foreground-color(White);
  print("world!\n")
}
