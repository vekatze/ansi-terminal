import {
  core.int.buffer,
  core.int.io,
  core.text.buffer,
  this.rule.ansi-kit {_ANSI-Kit, _ANSI-Kit-Unbuffered, ansi-kit},
  this.rule.color {Color-16, Color-256, Color-RGB},
  this.rule.color-ansi {
    Black,
    Blue,
    Cyan,
    Green,
    Magenta,
    Red,
    White,
    Yellow,
  },
  this.rule.color-intensity {Dull, Vivid},
  this.rule.color-layer {Background, Foreground},
  this.rule.command {Erase, Move-Cursor, Set-Style, command},
  this.rule.cursor-movement {Backward, Down, Down-Start, Forward, To, To-Column, Up, Up-Start},
  this.rule.space {Line, Screen},
  this.rule.span {From-Cursor, To-Cursor, Whole},
  this.rule.style {Bold, Color, Faint, Italic, Normal, Underline},
}

define write(k: &ansi-kit, t: &text): unit {
  case k {
  | _ANSI-Kit of {buffer} =>
    core.text.buffer.write(buffer, t)
  | _ANSI-Kit-Unbuffered of {sink} =>
    let _ = core.text.io.write(*sink, t);
    Unit
  }
}

define write-int(k: &ansi-kit, x: int): unit {
  case k {
  | _ANSI-Kit of {buffer} =>
    core.int.buffer.write-int(buffer, x);
  | _ANSI-Kit-Unbuffered of {sink} =>
    let _ = core.int.io.write-int(*sink, x);
    Unit
  }
}

inline write-code(k: &ansi-kit, c: command): unit {
  match c {
  | Set-Style(style) =>
    match style {
    | Normal =>
      write(k, "\u{1b}[0m")
    | Bold =>
      write(k, "\u{1b}[1m")
    | Faint =>
      write(k, "\u{1b}[2m")
    | Italic =>
      write(k, "\u{1b}[3m")
    | Underline =>
      write(k, "\u{1b}[4m")
    | Color(color-layer, color) =>
      match color-layer {
      | Foreground =>
        match color {
        | Color-16(color-intensity, color) =>
          match color-intensity {
          | Dull =>
            match color {
            | Black =>
              write(k, "\u{1b}[30m")
            | Red =>
              write(k, "\u{1b}[31m")
            | Green =>
              write(k, "\u{1b}[32m")
            | Yellow =>
              write(k, "\u{1b}[33m")
            | Blue =>
              write(k, "\u{1b}[34m")
            | Magenta =>
              write(k, "\u{1b}[35m")
            | Cyan =>
              write(k, "\u{1b}[36m")
            | White =>
              write(k, "\u{1b}[37m")
            }
          | Vivid =>
            match color {
            | Black =>
              write(k, "\u{1b}[90m")
            | Red =>
              write(k, "\u{1b}[91m")
            | Green =>
              write(k, "\u{1b}[92m")
            | Yellow =>
              write(k, "\u{1b}[93m")
            | Blue =>
              write(k, "\u{1b}[94m")
            | Magenta =>
              write(k, "\u{1b}[95m")
            | Cyan =>
              write(k, "\u{1b}[96m")
            | White =>
              write(k, "\u{1b}[97m")
            }
          }
        | Color-256(value) =>
          write(k, "\u{1b}[38;5;");
          write-int(k, zext-int8-int(value));
          write(k, "m");
        | Color-RGB(r, g, b) =>
          write(k, "\u{1b}[38;2;");
          write-int(k, zext-int8-int(r));
          write(k, ";");
          write-int(k, zext-int8-int(g));
          write(k, ";");
          write-int(k, zext-int8-int(b));
          write(k, "m");
        }
      | Background =>
        match color {
        | Color-16(color-intensity, color) =>
          match color-intensity {
          | Dull =>
            match color {
            | Black =>
              write(k, "\u{1b}[40m")
            | Red =>
              write(k, "\u{1b}[41m")
            | Green =>
              write(k, "\u{1b}[42m")
            | Yellow =>
              write(k, "\u{1b}[43m")
            | Blue =>
              write(k, "\u{1b}[44m")
            | Magenta =>
              write(k, "\u{1b}[45m")
            | Cyan =>
              write(k, "\u{1b}[46m")
            | White =>
              write(k, "\u{1b}[47m")
            }
          | Vivid =>
            match color {
            | Black =>
              write(k, "\u{1b}[100m")
            | Red =>
              write(k, "\u{1b}[101m")
            | Green =>
              write(k, "\u{1b}[102m")
            | Yellow =>
              write(k, "\u{1b}[103m")
            | Blue =>
              write(k, "\u{1b}[104m")
            | Magenta =>
              write(k, "\u{1b}[105m")
            | Cyan =>
              write(k, "\u{1b}[106m")
            | White =>
              write(k, "\u{1b}[107m")
            }
          }
        | Color-256(value) =>
          write(k, "\u{1b}[48;5;");
          write-int(k, zext-int8-int(value));
          write(k, "m");
        | Color-RGB(r, g, b) =>
          write(k, "\u{1b}[48;2;");
          write-int(k, zext-int8-int(r));
          write(k, ";");
          write-int(k, zext-int8-int(g));
          write(k, ";");
          write-int(k, zext-int8-int(b));
          write(k, "m");
        }
      }
    }
  | Move-Cursor(movement) =>
    match movement {
    | Up(offset) =>
      write(k, "\u{1b}[");
      write-int(k, offset);
      write(k, "A");
    | Down(offset) =>
      write(k, "\u{1b}[");
      write-int(k, offset);
      write(k, "B");
    | Forward(offset) =>
      write(k, "\u{1b}[");
      write-int(k, offset);
      write(k, "C");
    | Backward(offset) =>
      write(k, "\u{1b}[");
      write-int(k, offset);
      write(k, "D");
    | Up-Start(offset) =>
      write(k, "\u{1b}[");
      write-int(k, offset);
      write(k, "E");
    | Down-Start(offset) =>
      write(k, "\u{1b}[");
      write-int(k, offset);
      write(k, "F");
    | To-Column(column) =>
      write(k, "\u{1b}[");
      write-int(k, column);
      write(k, "G");
    | To(x, y) =>
      write(k, "\u{1b}[");
      write-int(k, x);
      write(k, ";");
      write-int(k, y);
      write(k, "H");
    }
  | Erase(space, span) =>
    match space {
    | Line =>
      match span {
      | From-Cursor =>
        write(k, "\u{1b}[0K")
      | To-Cursor =>
        write(k, "\u{1b}[1K")
      | Whole =>
        write(k, "\u{1b}[2K")
      }
    | Screen =>
      match span {
      | From-Cursor =>
        write(k, "\u{1b}[0J")
      | To-Cursor =>
        write(k, "\u{1b}[1J")
      | Whole =>
        write(k, "\u{1b}[2J")
      }
    }
  }
}
